# Maintainer: gyaru
pkgname=termite-w3mfix
pkgver=11
pkgrel=1
pkgdesc="A simple VTE-based terminal with a lazy fix for w3m support. (transparency removed)"
_gitname="termite"
arch=('i686' 'x86_64')
url="https://github.com/thestinger/termite/"
license=('LGPL')
depends=('vte3-select-text>=0.38.0-1')
makedepends=('git')
provides=(termite)
conflicts=(termite)
backup=(etc/xdg/termite/config)
patches=('lazyw3mfix.diff')
source=("git://github.com/thestinger/termite#tag=v$pkgver" "lazyw3mfix.diff")
md5sums=('SKIP'
        'ba273b1114ca1a21eecda46544cc53df')

prepare() {
    cd $_gitname
    git submodule update --init
    patch -p0 < "$srcdir/lazyw3mfix.diff"
}

build() {
  cd $srcdir/$_gitname
  make
}

package() {
  cd $srcdir/$_gitname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 config "$pkgdir/etc/xdg/termite/config"
}
